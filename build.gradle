plugins {
    id 'war'
    id "com.diffplug.spotless" version "6.25.0"
}
description = 'System Readiness Manager app'
group 'org.jlab'
version new File("${projectDir}/VERSION").text.trim()
ext.releaseDate = new Date().format('MMM dd yyyy')

tasks.withType(JavaCompile) {
    options.release = 11
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
}
repositories {
    mavenCentral()
}
dependencies {
    // No matter what smoothness weblib MUST be included in WAR (https://github.com/JeffersonLab/smoothness/issues/4)
    implementation 'org.jlab:smoothness-weblib:4.11.0'

    // Smoothness setup should install these into Wildfly
    providedCompile 'org.tuckey:urlrewritefilter:4.0.4',
            'org.jlab:jlog:5.0.0',
            'org.apache.poi:poi:5.2.3',
            'org.apache.poi:poi-ooxml:5.2.3',
            'com.oracle.database.jdbc:ojdbc11:21.7.0.0'

    // Wildfly 26.1.3.Final provides:
    providedCompile 'javax:javaee-api:8.0.1',
            'javax.servlet:jstl:1.2',
            'org.hibernate:hibernate-core:5.3.28.Final',
            'org.hibernate:hibernate-envers:5.3.28.Final',
            'org.jsoup:jsoup:1.14.2'
}
war {
    archiveFileName = 'srm.war'
    filesMatching('WEB-INF/web.xml') {
        filter {
            String line -> line.replaceAll("@VERSION@", project.version)
        }
        filter {
            String line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
    }
}
spotless {
    java {
        googleJavaFormat()
    }
}